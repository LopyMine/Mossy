plugins {
	id "fabric-loom" version "1.6-SNAPSHOT"
	id "dev.kikugie.j52j" version "1.0.2"
	id "me.modmuss50.mod-publish-plugin" version "0.5.+"
}

def modId = project.mod_id
def modName = project.mod_name
def modVersion = project.mod_version
def modMavenGroup = project.mod_group

def modDescription = project.mod_description
def modAuthors = project.mod_authors
def modLicense = project.mod_license

def minecraftVersion = stonecutter.current.version.toString() // Current MC version from "./versions/" folder
def versionsWithSupportRange = getVersionsWithSupportRange()
def supportedMinecraftVersionRange = getSupportedMinecraftVersionRange(versionsWithSupportRange, minecraftVersion)
println "[${minecraftVersion}] Supported Minecraft Versions: ${supportedMinecraftVersionRange}"
def lombokVersion = project.lombok_version
def fabricLoader = findProperty("build.fabric_loader")

def javaVersionAsInt = getJavaVersionAsInteger(minecraftVersion)
def javaVersion = JavaVersion.toVersion(javaVersionAsInt)
println "[${minecraftVersion}] Using Java: ${javaVersion.toString()}"
def fabricAPI = findProperty("build.fabric_api")

version = "${modVersion}+${supportedMinecraftVersionRange}"
group = modMavenGroup

j52j {
	sources(sourceSets["main"])
}

base {
	archivesName = modName
}

repositories {
	maven { url "https://maven.quiltmc.org/repository/release/" } // Stil for YACL
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" } // Stil for YACL
	maven {
		name "Terraformers" // ModMenu
		url "https://maven.terraformersmc.com/"
	}
	maven {
		name 'Xander Maven' // YACL
		url 'https://maven.isxander.dev/releases'
	}
	maven {
		url "https://maven.nucleoid.xyz/"
		name "Nucleoid"
	}
	// For Mod Menu, because at 1.20.4 it needs PlaceHolder API, see https://github.com/TerraformersMC/ModMenu/issues/752
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	mappings "net.fabricmc:yarn:${findProperty("build.yarn")}:v2"
	modImplementation "net.fabricmc:fabric-loader:${fabricLoader}"

	// Lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricAPI}"

	// Mod Menu
	modImplementation "com.terraformersmc:modmenu:${findProperty("dep.modmenu")}"

	// YACL
	def minecraftVersionsWithOldYACLMaven = Set.of("1.19.4", "1.20", "1.20.2", "1.20.3")
	if (minecraftVersionsWithOldYACLMaven.contains(minecraftVersion)) {
		def property = findProperty("dep.yacl").toString()
		def i = property.lastIndexOf("-")
		if (i != -1) {
			property = property.substring(0, i)
		}
		modImplementation "dev.isxander.yacl:yet-another-config-lib-fabric:${property}"
	} else {
		modImplementation "dev.isxander:yet-another-config-lib:${findProperty("dep.yacl")}"
	}
}

loom {
	mixin {
		defaultRefmapName.set("${modId}.refmap.json")
	}

	def accessWidenerFile = project.rootProject.file("src/main/resources/aws/${minecraftVersion}.accesswidener")
	if (accessWidenerFile.exists()) {
		accessWidenerPath = accessWidenerFile
	}

	def playerProperties = getPlayerProperties()

	def playerNicknameProperty = playerProperties.get("player_nickname").toString() // Your in-game nickname
	def playerNickname = getNickname(playerNicknameProperty)

	def playerUuidProperty = playerProperties.get("player_uuid").toString() // Your in-game uuid
	def playerUuid = getUUID(playerUuidProperty, playerUuidProperty.indexOf("-") == -1)

	runConfigs.configureEach { config ->
		config.setIdeConfigGenerated(true) // generate IDE tasks for running client, server and testmod, datagen if those are present
		config.setRunDir("../../runs/${config.environment}") // use a global run directory for all versions

		if (config.getEnvironment() == "client") { // setting player properties from "./players/player.properties" file
			if (playerNickname != null) {
				config.programArgs("--username", playerNickname.toString())
			}
			if (playerUuid != null) {
				config.programArgs("--uuid", playerUuid.toString())
			}
		}
	}
}

processResources {
	def props = [:]
	props.put("mod_id", modId.toString())
	props.put("mod_authors", modAuthors.toString())
	props.put("mod_group", modMavenGroup)
	props.put("mod_description", modDescription.toString())
	props.put("mod_name", modName.toString())
	props.put("mod_license", modLicense.toString())
	props.put("version", version.toString())
	props.put("fabric_loader", fabricLoader.toString())
	props.put("minecraft", minecraftVersion.toString())
	props.put("java", javaVersionAsInt.toString())
	props.put("fabric_api", fabricAPI.toString())
	props.put("fabric_api_id", stonecutter.compare("1.19.1", minecraftVersion) >= 0 ? "fabric" : "fabric-api")

	props.each { key, value ->
		inputs.property key, value
	}

	filesMatching(["fabric.mod.json"]) {
		expand(props)
	}

	filesMatching("aws/*.accesswidener") {
		if (!it.getName().startsWith(minecraftVersion)) {
			it.exclude()
		}
	}

	filesMatching("mixins/*.mixins.json5") {
		def fileName = it.getName().toString()
		def index = fileName.indexOf('-')
		if (index != -1 && fileName.substring(0, index) == minecraftVersion) {
			expand(props)
		} else {
			it.exclude()
		}
	}
}

stonecutter {
	swap("mod_version", "\"${findProperty("mod_version")}\";")
	swap("mod_id", "\"${findProperty("mod_id")}\";")
	swap("mod_name", "\"${findProperty("mod_name")}\";")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = (javaVersionAsInt as int)
}

tasks.register('buildAndCollect', Copy) {
	group = 'build'
	from(tasks.remapJar.archiveFile)
	into(rootProject.layout.buildDirectory.file("libs/"))
	dependsOn('build')
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}" }
	}
}

static String getSupportedMinecraftVersionRange(VersionWithSupportRange[] versionsWithSupportRange, String currentMinecraftVersion) {
	def result = versionsWithSupportRange.findResult { v ->
		def min = v.minVersion.toString()
		def max = v.maxVersion.toString()
		if (v.projectVersion == currentMinecraftVersion && min != max) {
			return "${min}-${max}".toString()
		}
		return null
	}
	return result == null ? currentMinecraftVersion : result
}

String getNickname(String nickname) {
	if (nickname == null) {
		return null
	}
	if (!nickname.matches('^[a-zA-Z0-9_]{2,16}$')) {
		println "WARNING — Your nickname is wrong, using default nickname 'Player'"
		return "Player"
	}
	return nickname
}

UUID getUUID(String uuid, boolean undashed) {
	if (uuid == null) {
		return null
	}
	try {
		if (undashed) {
			return UUID.fromString(uuid.replaceFirst("(\\w{8})(\\w{4})(\\w{4})(\\w{4})(\\w{12})", '$1-$2-$3-$4-$5'))
		}
		return UUID.fromString(uuid)
	} catch (Exception e) {
		e.printStackTrace()
	}
	println "WARNING — Your UUID is wrong, failed to parse it"
	return null
}

// Cannot be static!
int getJavaVersionAsInteger(String minecraftVersion) {
	return stonecutter.compare("1.20.5", minecraftVersion) == 1 ?
			stonecutter.compare("1.18", minecraftVersion) == 1 ?
					stonecutter.compare("1.17", minecraftVersion) == 1 ?
							8
							:
							16
					:
					17
			:
			21
}

Properties getPlayerProperties() {
	def playerProperties = new Properties()
	def playerPropertiesFile = project.rootProject.file("player/player.properties")
	if (playerPropertiesFile.exists()) {
		playerPropertiesFile.withInputStream { stream ->
			playerProperties.load(stream)
		}
	} else {
		println "Failed to find player properties at ${playerPropertiesFile}"
	}
	return playerProperties
}

Properties getPublicationProperties() {
	def properties = new Properties()
	def publicationPropertiesFile = project.rootProject.file("publish/publish.properties")
	if (publicationPropertiesFile.exists()) {
		publicationPropertiesFile.withInputStream { stream ->
			properties.load(stream)
		}
	}
	return properties
}

VersionWithSupportRange[] getVersionsWithSupportRange() {
	def pubVersions = []
	def versions = project.multi_versions.toString().split(" ")

	versions.each { version ->
		if (version.contains("[")) {
			def range = version.substring(0, version.length() - 1).split("\\[")
			if (range.size() == 2) {
				def min = range[0]
				def max = range[1]
				if (!max.contains(".")) {
					def i = min.lastIndexOf(".")
					if (i != -1) {
						max = min.substring(0, i + 1) + max
					} else {
						max = min + "." + max
					}
				}
				pubVersions.add(new VersionWithSupportRange(projectVersion: min, minVersion: min, maxVersion: max))
			}
		} else {
			pubVersions.add(new VersionWithSupportRange(projectVersion: version, minVersion: version, maxVersion: version))
		}
	}

	return pubVersions
}

def getBuildedModFileOrDefault(String modName, String modVersion, String minecraftVersion) {
	def path = "libs/${modName}-${modVersion}+${minecraftVersion}.jar"
	def provider = project.rootProject.layout.buildDirectory.file(path)
	def regularFile = provider.orNull
	if (regularFile == null || !regularFile.getAsFile().exists()) {
		println "[PublishMods] [WARNING] Failed to find jar file for ${minecraftVersion} at '${path}'! Using archive file!"
		return remapJar.archiveFile
	}
	return regularFile.getAsFile()
}

String getChangeLog() {
	def file = project.rootProject.file("publish/CHANGELOG.md")
	if (file.exists()) {
		def text = file.getText()
		if (!text.isBlank()) {
			return text
		}
		println "[PublishMods] [WARNING] No changelog provided!"
	}
	println "[PublishMods] [WARNING] Failed to find changelog file at '${file.path}'!"
	return "No changelog specified."
}

publishMods {
	def pubProperties = getPublicationProperties()
	if (pubProperties.isEmpty()) {
		println "[PublishMods] [WARNING] Failed to publish.properties at ${publicationPropertiesFile}"
		println "[PublishMods] [WARNING] Configuration canceled"
		return
	}
	def pubVersions = versionsWithSupportRange
	def modrinthProjectId = pubProperties.get("modrinth_id").toString()
	def curseForgeProjectId = pubProperties.get("curseforge_id").toString()
	def maxJavaVersion = JavaVersion.toVersion(pubProperties.get("max_java_version"))
	def isClient = Boolean.parseBoolean(pubProperties.get("is_for_client").toString())
	def isDry = Boolean.parseBoolean(pubProperties.get("imitation").toString())
	def isServer = Boolean.parseBoolean(pubProperties.get("is_for_server").toString())
	def requiresDepends = pubProperties.get("requires").toString().split(" ")
	def optionalDepends = pubProperties.get("optional").toString().split(" ")
	def incompatibleDepends = pubProperties.get("incompatible").toString().split(" ")
	def embedsDepends = pubProperties.get("embeds").toString().split(" ")
	def loaders = pubProperties.get("loaders").toString().split(" ")

	def versionTypeProperty = pubProperties.get("version_type").toString()
	def versionType
	if (versionTypeProperty == "RELEASE") {
		versionType = STABLE
	} else if (versionTypeProperty == "BETA") {
		versionType = BETA
	} else if (versionTypeProperty == "ALPHA") {
		versionType = ALPHA
	} else {
		return
	}

	VersionWithSupportRange pubVersion = null
	def publish = pubVersions.any {
		def bl = it.projectVersion == minecraftVersion
		if (bl) {
			pubVersion = it
		}
		return bl
	}

	if (publish) {
		def versionName = "[${supportedMinecraftVersionRange}] ${modName} ${modVersion}"
		displayName = versionName
		println "[PublishMods] [INFO] Configuring '${versionName}' for publishing"
		file = getBuildedModFileOrDefault(modName, modVersion, minecraftVersion)
		changelog = getChangeLog()
		type = versionType
		modLoaders.addAll(loaders)

		def bl = providers.environmentVariable("CURSEFORGE_API_KEY").getOrNull() == null
		def bl2 = providers.environmentVariable("MODRINTH_API_KEY").getOrNull() == null
		def bl3 = isDry || bl || bl2
		dryRun = bl3
		if (bl3) {
			println "[PublishMods] [WARNING] Dry Run is Enabled!"
		}

		curseforge {
			projectId = curseForgeProjectId
			accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")

			for (i in javaVersion.ordinal()..<maxJavaVersion.ordinal() + 1) {
				javaVersions.add(JavaVersion.values()[i])
			}

			clientRequired = isClient
			serverRequired = isServer

			if (pubVersion.minVersion != pubVersion.maxVersion) {
				minecraftVersionRange {
					start = pubVersion.minVersion
					end = pubVersion.maxVersion
				}
			} else {
				minecraftVersions.add(minecraftVersion)
			}

			if (requiresDepends.first() != "none") {
				requires(requiresDepends)
			}
			if (optionalDepends.first() != "none") {
				optional(optionalDepends)
			}
			if (incompatibleDepends.first() != "none") {
				incompatible(incompatibleDepends)
			}
			if (embedsDepends.first() != "none") {
				embeds(embedsDepends)
			}
		}

		modrinth {
			projectId = modrinthProjectId
			accessToken = providers.environmentVariable("MODRINTH_API_KEY")

			if (pubVersion.minVersion != pubVersion.maxVersion) {
				minecraftVersionRange {
					start = pubVersion.minVersion
					end = pubVersion.maxVersion
				}
			} else {
				minecraftVersions.add(minecraftVersion)
			}

			if (requiresDepends.first() != "none") {
				requires(requiresDepends)
			}
			if (optionalDepends.first() != "none") {
				optional(optionalDepends)
			}
			if (incompatibleDepends.first() != "none") {
				incompatible(incompatibleDepends)
			}
			if (embedsDepends.first() != "none") {
				embeds(embedsDepends)
			}
		}
	}
}

class VersionWithSupportRange {
	def projectVersion
	def minVersion
	def maxVersion

	String toString() {
		if (minVersion != maxVersion) {
			return "${projectVersion}[${minVersion}-${maxVersion}]"
		}
		return projectVersion
	}
}